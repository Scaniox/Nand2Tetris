// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   Mux16(a=x, b[0..7]=false, sel=zx, out=xOr0); // zero x if zx
   Not16(in=xOr0, out=notxOr0); // not of xOr0
   Mux16(a=xOr0, b=notxOr0, sel=nx, out=prepedx); // choose to xOr0 or negxOr0

   Mux16(a=y, b[0..7]=false, sel=zy, out=yOr0); // zero y if zy
   Not16(in=yOr0, out=notyOr0); // not of yOr0
   Mux16(a=yOr0, b=notyOr0, sel=ny, out=prepedy); // choose to yOr0 or negyOr0

   And16(a=prepedx, b=prepedy, out=xyAnd);
   Add16(a=prepedx, b=prepedy, out=xySum);
   Mux16(a=xyAnd, b=xySum, sel=f, out=computeOut); // select between And and adding x and y

   Not16(in=computeOut, out=notComputeOut); // not of compute output
   Mux16(a=computeOut, b=notComputeOut, sel=no, out=out, out[15]=ng, out[0..7]=low8, out[8..15]=top8); // select if compute out is inverted


   Or8Way(in=low8, out=low8Ored);
   Or8Way(in=top8, out=top8Ored);
   Or(a=low8Ored, b=top8Ored, out=Or16way); // 16 way OR to see if any of the output bits are on
   Not(in=Or16way, out=zr); // notted to be high with 0

}
